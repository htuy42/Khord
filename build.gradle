buildscript {
    ext.kotlin_version = '1.3.61'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

def grpcVersion = '1.27.2' // CURRENT_GRPC_VERSION

repositories {
    mavenCentral()
    flatDir{
        dirs 'd:/dev/lib'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "com.google.api.grpc:proto-google-common-protos:0.1.9"

    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "com.google.guava:guava:28.2-jre"
    compile "javax.annotation:javax.annotation-api:1.3.2"
    compile "com.htuy:socket:1.0"
    testCompile group: 'junit', name: 'junit', version:'4.12'

    compile('org.apache.curator:curator-recipes:4.3.0') {
        exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    }
    compile group: 'org.apache.zookeeper', name: 'zookeeper', version: '3.4.7'
}


protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                // To generate deprecated interfaces and static bindService method,
                // turn the enable_deprecated option to true below:
                option 'enable_deprecated=false'
            }
        }
    }
}

idea {
    module {
        // Not using generatedSourceDirs because of
        // https://discuss.gradle.org/t/support-for-intellij-2016/15294/8
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java")
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc")
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// auto build protobufs
compileKotlin.dependsOn ':generateProto'

sourceSets.main.java.srcDirs += 'build/generated/source/proto/main/grpc'
sourceSets.main.java.srcDirs += 'build/generated/source/proto/main/java'

jar{
    manifest {
        attributes(
                'Main-Class': 'com.htuy.chord.MainKt'
        )
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    destinationDir = new File("d:/bin/jars")
}